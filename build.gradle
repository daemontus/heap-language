buildscript {
    allprojects {   // Set graal version and some global properties in all projects
        ext.graalVersion = "20.1.0"
        ext.libProfilerVersion = "RELEASE113"
        ext.junitVersion = "4.12"

        repositories {
            jcenter()
            mavenCentral()
        }

        project.afterEvaluate {
            java { sourceCompatibility JavaVersion.VERSION_1_8 }
            // Workaround to enable JaCoCo on Graal (https://bugs.openjdk.java.net/browse/JDK-8245283)
            test { jvmArgs "-Djfr.unsupported.vm=true" }
        }
    }
}

plugins {
    id 'application'
    id 'java-library'
    id 'jacoco'
    id 'org.graalvm.plugin.compiler' version '0.1.0-alpha2'
}

dependencies {
    // JS support in non-Graal VMs
    language project(":heap")
    installedLanguage "org.graalvm.js:js:$graalVersion"
    installedLanguage "org.graalvm.js:js-scriptengine:$graalVersion"

    // Heap analysis library from Netbeans
    implementation "org.graalvm.sdk:graal-sdk:$graalVersion"
    implementation "org.netbeans.modules:org-netbeans-modules-profiler-oql:$libProfilerVersion"

    testImplementation 'junit:junit:4.12'
}

run {
    systemProperty "engine", System.getProperty("engine")   // pass engine property to script runner
}

application {
    mainClassName = 'com.oracle.truffle.heap.ScriptRunner'
}